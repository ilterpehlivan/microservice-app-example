plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.19'
}

repositories {
  mavenCentral()
}


def protoVersion = "3.18.2"
def grpcVersion = "1.41.0"
def reactiveGrpcVersion = "1.0.0"


dependencies {
    implementation "com.google.protobuf:protobuf-java:$protoVersion"
    implementation "com.google.protobuf:protobuf-java-util:$protoVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    runtimeOnly "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-services:$grpcVersion"
    implementation "com.salesforce.servicelibs:reactor-grpc-stub:${reactiveGrpcVersion}"
//    compile "org.springframework.boot:spring-boot-starter-logging:${springBootDependenciesVersion}"
    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        compileOnly 'javax.annotation:javax.annotation-api:1.3.1'
    }
    testImplementation "io.grpc:grpc-testing:$grpcVersion"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protoVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${reactiveGrpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            reactor {}
        }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/reactor'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

